FORM adjust_display_fields_by_state.

  FIELD-SYMBOLS: <ls_disp> TYPE ty_task_disp.

  LOOP AT gt_display ASSIGNING <ls_disp>.

    "-------------------------------------------------------------*
    " A) Show Char = No, Show Long Text = No
    "    → Operations only
    "-------------------------------------------------------------*
    IF gv_show_char = gc_false AND gv_show_ltxt = gc_false.

      "No long-text columns in this mode
      CLEAR: <ls_disp>-tdformat,
             <ls_disp>-op_ltxt,
             <ls_disp>-char_ltxt.

      "Insp# + Description cleared on op rows
      IF <ls_disp>-row_kind = 'O'.
        CLEAR: <ls_disp>-merknr,
               <ls_disp>-kurzt_char.
        "PMETHODE stays (per FDS)
      ENDIF.

    "-------------------------------------------------------------*
    " B) Show Char = Yes, Show Long Text = No
    "    → Operation + characteristic rows, no long text at all
    "-------------------------------------------------------------*
    ELSEIF gv_show_char = gc_true AND gv_show_ltxt = gc_false.

      CLEAR: <ls_disp>-tdformat,
             <ls_disp>-op_ltxt,
             <ls_disp>-char_ltxt.

    "-------------------------------------------------------------*
    " C) Show Char = No, Show Long Text = Yes
    "    → Operation rows + operation long-text rows
    "       FK + OP_LTXT only, no characteristic info
    "-------------------------------------------------------------*
    ELSEIF gv_show_char = gc_false AND gv_show_ltxt = gc_true.

      CLEAR: <ls_disp>-merknr,
             <ls_disp>-kurzt_char,
             <ls_disp>-pmethode,
             <ls_disp>-char_ltxt.
      "TDFORMAT + OP_LTXT stay filled for long-text rows

    "-------------------------------------------------------------*
    " D) Show Char = Yes, Show Long Text = Yes
    "    → Operation + characteristic rows + characteristic LT rows
    "       Header rows: Insp#/Desc/Method only
    "       LT rows:     FK + Char Long Text only
    "-------------------------------------------------------------*
    ELSE.  "gv_show_char = TRUE AND gv_show_ltxt = TRUE

      IF <ls_disp>-row_kind = 'L'.
        "Characteristic long-text row
        "→ show only FK + CHAR_LTXT
        CLEAR: <ls_disp>-merknr,
               <ls_disp>-kurzt_char,
               <ls_disp>-pmethode,
               <ls_disp>-op_ltxt.
      ELSE.
        "Operation or characteristic header row
        "→ no long-text columns here
        CLEAR: <ls_disp>-tdformat,
               <ls_disp>-op_ltxt,
               <ls_disp>-char_ltxt.
      ENDIF.

    ENDIF.

  ENDLOOP.

ENDFORM.