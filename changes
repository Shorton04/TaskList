FORM append_char_longtext USING is_base TYPE ty_task_base.

  DATA: lt_lines TYPE STANDARD TABLE OF tline,
        ls_line  TYPE tline,
        lv_name  TYPE thead-tdname,
        lv_merknr TYPE plmk-merknr.

  IF is_base-ltextkz IS INITIAL.
    RETURN.
  ENDIF.

  lv_merknr = |{ is_base-merknr ALPHA = IN }|.

  CONCATENATE sy-mandt
              is_base-plnty
              is_base-plnnr
              is_base-plnkn
              lv_merknr
         INTO lv_name.

  CALL FUNCTION 'READ_TEXT'
    EXPORTING
      client   = sy-mandt
      id       = 'QM'
      language = sy-langu
      name     = lv_name
      object   = 'QSS'
    TABLES
      lines    = lt_lines
    EXCEPTIONS
      OTHERS   = 4.

  IF lt_lines IS INITIAL.
    RETURN.
  ENDIF.

  LOOP AT lt_lines INTO ls_line.
    CLEAR gs_display.
    MOVE-CORRESPONDING is_base TO gs_display.

    gs_display-row_kind  = 'L'.
    gs_display-char_ltxt = ls_line-tdline.
    gs_display-tdformat  = ls_line-tdformat.

    APPEND gs_display TO gt_display.
  ENDLOOP.

ENDFORM.