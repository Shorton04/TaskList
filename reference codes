FORM display_alv.

  IF gt_display IS INITIAL.
    MESSAGE s398(00) WITH 'No data to display'.
    RETURN.
  ENDIF.

  IF lo_alv IS INITIAL.

    TRY.
        cl_salv_table=>factory(
          IMPORTING r_salv_table = lo_alv
          CHANGING  t_table      = gt_display ).

        lo_columns = lo_alv->get_columns( ).
        lo_columns->set_optimize( abap_true ).

        "Enable row color
        lo_columns->set_color_column( 'LINE_COLOR' ).

      CATCH cx_salv_not_found cx_salv_data_error cx_salv_msg.
        RETURN.
    ENDTRY.

    "=== Layout ===
    lo_layout = lo_alv->get_layout( ).
    DATA(ls_key) = VALUE salv_s_layout_key( report = sy-repid ).
    lo_layout->set_key( ls_key ).
    lo_layout->set_default( abap_true ).
    lo_layout->set_save_restriction( if_salv_c_layout=>restrict_none ).

    "=== Toolbar Functions ===
    lo_funcs = lo_alv->get_functions( ).
    lo_funcs->set_all( abap_true ).

    "=== Standard SALV Row Selector ===
    DATA(lo_sel) = lo_alv->get_selections( ).
    lo_sel->set_selection_mode( if_salv_c_selection_mode=>row ).
    " ^^^ THIS SHOWS THE STANDARD SALV LEFT SELECT COLUMN

    "=== Register Events ===
    lo_events = lo_alv->get_event( ).
    SET HANDLER lcl_event_handler=>on_user_command FOR lo_events.
    SET HANDLER lcl_event_handler=>on_link_click   FOR lo_events.

  ELSE.
    lo_alv->refresh( ).
  ENDIF.

  "=== Column position ===
  PERFORM set_position USING 'PLNNR'      1.
  PERFORM set_position USING 'PLNAL'      2.
  PERFORM set_position USING 'WERKS'      3.
  PERFORM set_position USING 'VORNR'      4.
  PERFORM set_position USING 'ARBPL_O'    5.
  PERFORM set_position USING 'ARBEI'      6.
  PERFORM set_position USING 'KURZT_CHAR' 7.
  PERFORM set_position USING 'PMETHODE'   8.
  PERFORM set_position USING 'MERKNR'     9.
  PERFORM set_position USING 'TDFORMAT'   10.
  PERFORM set_position USING 'OP_LTXT'    11.
  PERFORM set_position USING 'CHAR_LTXT'  12.

  PERFORM set_column_visibility_by_state.
  PERFORM set_pfstatus_from_state.
  PERFORM set_ci_headers.

  lo_alv->display( ).

ENDFORM.