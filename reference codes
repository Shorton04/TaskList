FORM build_display.

  DATA: ls_base        TYPE ty_task_base,
        ls_disp        TYPE ty_task_disp,
        lv_last_plnty  TYPE plnty,
        lv_last_plnnr  TYPE plnnr,
        lv_last_plnal  TYPE plnal,
        lv_last_vornr  TYPE plnkn,
        lv_last_merknr TYPE merknr,
        ls_color       TYPE lvc_s_scol,
        lv_has_char    TYPE abap_bool.

  CLEAR: gt_display,
         lv_last_plnty,
         lv_last_plnnr,
         lv_last_plnal,
         lv_last_vornr,
         lv_last_merknr.

  SORT gt_base BY plnty plnnr plnal vornr merknr.

  LOOP AT gt_base INTO ls_base.

    "===========================================================
    "  NEW OPERATION ROW
    "===========================================================
    IF ls_base-vornr <> lv_last_vornr
    OR ls_base-plnnr <> lv_last_plnnr
    OR ls_base-plnal <> lv_last_plnal
    OR ls_base-plnty <> lv_last_plnty.

      "Check if this operation actually has characteristics
      lv_has_char = abap_false.
      LOOP AT gt_base TRANSPORTING NO FIELDS
           WHERE plnty = ls_base-plnty
             AND plnnr = ls_base-plnnr
             AND plnal = ls_base-plnal
             AND vornr = ls_base-vornr
             AND merknr IS NOT INITIAL.
        lv_has_char = abap_true.
        EXIT.
      ENDLOOP.

      "If user wants characteristics only â†’ skip ops without characteristics
      IF gv_show_char = abap_true AND lv_has_char = abap_false.
        CONTINUE.
      ENDIF.

      "Build OPERATION row
      CLEAR ls_disp.
      MOVE-CORRESPONDING ls_base TO ls_disp.
      ls_disp-row_kind = 'O'.

      "============== REMOVE ALL CI FIELDS FROM OP ROW ==============
      CLEAR: ls_disp-merknr,
             ls_disp-kurzt_char,
             ls_disp-pmethode,
             ls_disp-verwmerkm,
             ls_disp-toleranzun,
             ls_disp-toleranzob,
             ls_disp-pruefeinh,
             ls_disp-stichprver,
             ls_disp-katalgart1,
             ls_disp-auswmenge1,
             ls_disp-steuerkz,
             ls_disp-ltextkz,
             ls_disp-char_ltxt,
             ls_disp-tdformat.

      DO 28 TIMES.
        ASSIGN COMPONENT |CI{ sy-index WIDTH = '02' }| OF STRUCTURE ls_disp TO FIELD-SYMBOL(<raw>).
        ASSIGN COMPONENT |CI{ sy-index WIDTH = '02' }_TXT| OF STRUCTURE ls_disp TO FIELD-SYMBOL(<txt>).
        IF <raw> IS ASSIGNED. CLEAR <raw>. ENDIF.
        IF <txt> IS ASSIGNED. CLEAR <txt>. ENDIF.
      ENDDO.

      APPEND ls_disp TO gt_display.

      "Append OP long text rows only when char OFF + long text ON
      IF gv_show_char = abap_false AND gv_show_ltxt = abap_true.
        PERFORM append_op_longtext USING ls_base.
      ENDIF.

      lv_last_plnty = ls_base-plnty.
      lv_last_plnnr = ls_base-plnnr;
      lv_last_plnal = ls_base-plnal.
      lv_last_vornr = ls_base-vornr.
      CLEAR lv_last_merknr.

    ENDIF.


    "===========================================================
    "  CHARACTERISTIC ROWS
    "===========================================================
    IF gv_show_char = abap_true
    AND ls_base-merknr IS NOT INITIAL.

      IF ls_base-merknr <> lv_last_merknr.

        CLEAR ls_disp.
        MOVE-CORRESPONDING ls_base TO ls_disp.
        ls_disp-row_kind = 'C'.

        "Here only we populate CI fields
        PERFORM fill_ci_fields USING ls_base-steuerkz CHANGING ls_disp.

        ls_color-color-col = 6.
        ls_color-color-int = 1.
        APPEND ls_color TO ls_disp-line_color.

        APPEND ls_disp TO gt_display.

        lv_last_merknr = ls_base-merknr.

      ENDIF.

      IF gv_show_ltxt = gc_true.
        PERFORM append_char_longtext USING ls_base.
      ENDIF.

    ENDIF.

  ENDLOOP.

ENDFORM.