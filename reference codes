FORM build_display.

  DATA: ls_base        TYPE ty_task_base,
        ls_disp        TYPE ty_task_disp,
        lv_last_plnty  TYPE plnty,
        lv_last_plnnr  TYPE plnnr,
        lv_last_plnal  TYPE plnal,
        lv_last_vornr  TYPE plnkn,
        lv_last_merknr TYPE merknr,
        ls_color       TYPE lvc_s_scol.

  CLEAR: gt_display,
         lv_last_plnty,
         lv_last_plnnr,
         lv_last_plnal,
         lv_last_vornr,
         lv_last_merknr.

  SORT gt_base BY plnty plnnr plnal vornr merknr.

  LOOP AT gt_base INTO ls_base.

    " NEW OPERATION GROUP
    IF ls_base-vornr <> lv_last_vornr
 OR ls_base-plnnr <> lv_last_plnnr
 OR ls_base-plnal <> lv_last_plnal
 OR ls_base-plnty <> lv_last_plnty.

      "============================================
      " SKIP OPERATION ROWS THAT HAVE NO CHARACTERISTICS
      " WHEN SHOW_CHAR = TRUE
      "============================================
      IF gv_show_char = abap_true.

        DATA(lv_has_char) = abap_false.

        LOOP AT gt_base TRANSPORTING NO FIELDS
             WHERE plnty  = ls_base-plnty
               AND plnnr  = ls_base-plnnr
               AND plnal  = ls_base-plnal
               AND vornr  = ls_base-vornr
               AND merknr IS NOT INITIAL.

          lv_has_char = abap_true.
          EXIT.
        ENDLOOP.

        IF lv_has_char = abap_false.
          CONTINUE.  "Skip this operation — no characteristics
        ENDIF.

      ENDIF.

      CLEAR ls_disp.
      MOVE-CORRESPONDING ls_base TO ls_disp.

      IF lv_has_char = abap_true.
        PERFORM fill_ci_fields USING ls_base-steuerkz CHANGING ls_disp.
      ELSE.
        " Operation has NO characteristics → clear EVERYTHING
        CLEAR: ls_disp-merknr,
               ls_disp-kurzt_char,
               ls_disp-pmethode,
               ls_disp-verwmerkm,
               ls_disp-toleranzun,
               ls_disp-toleranzob,
               ls_disp-pruefeinh,
               ls_disp-stichprver,
               ls_disp-katalgart1,
               ls_disp-auswmenge1,
               ls_disp-steuerkz,
               ls_disp-ltextkz,
               ls_disp-char_ltxt,
               ls_disp-tdformat.

        " Clear CI01–CI28 and CIx_TXT
        DO 28 TIMES.
          ASSIGN COMPONENT |CI{ sy-index WIDTH = 2 PAD = '0' }| OF STRUCTURE ls_disp TO FIELD-SYMBOL(<ci_raw>).
          ASSIGN COMPONENT |CI{ sy-index WIDTH = 2 PAD = '0' }_TXT| OF STRUCTURE ls_disp TO FIELD-SYMBOL(<ci_txt>).

          IF <ci_raw> IS ASSIGNED. CLEAR <ci_raw>. ENDIF.
          IF <ci_txt> IS ASSIGNED. CLEAR <ci_txt>. ENDIF.
        ENDDO.
      ENDIF.

      APPEND ls_disp TO gt_display.

      ls_disp-row_kind = 'O'.
      "--- Format Upper / Lower Spec for ALV (FLTP → CHAR)
      DATA: lv_tolun_p   TYPE p LENGTH 16 DECIMALS 9,
            lv_tolob_p   TYPE p LENGTH 16 DECIMALS 9,
            lv_tolun_txt TYPE c LENGTH 20,
            lv_tolob_txt TYPE c LENGTH 20.

      CLEAR: lv_tolun_p, lv_tolob_p, lv_tolun_txt, lv_tolob_txt.

      "--- Lower Spec Limit
      IF ls_base-toleranzun IS NOT INITIAL.
        lv_tolun_p = ls_base-toleranzun.
        WRITE lv_tolun_p TO lv_tolun_txt DECIMALS 9.
        ls_disp-toleranzun = lv_tolun_txt.
      ENDIF.

      "--- Upper Spec Limit
      IF ls_base-toleranzob IS NOT INITIAL.
        lv_tolob_p = ls_base-toleranzob.
        WRITE lv_tolob_p TO lv_tolob_txt DECIMALS 9.
        ls_disp-toleranzob = lv_tolob_txt.
      ENDIF.

      " Fill CI fields here
      PERFORM fill_ci_fields USING ls_base-steuerkz CHANGING ls_disp.

      APPEND ls_disp TO gt_display.

      lv_last_plnty = ls_base-plnty.
      lv_last_plnnr = ls_base-plnnr.
      lv_last_plnal = ls_base-plnal.
      lv_last_vornr = ls_base-vornr.

      CLEAR lv_last_merknr.
      "Operation long text (if char OFF and longtext ON)
      IF gv_show_char = abap_false AND gv_show_ltxt = abap_true.
        PERFORM append_op_longtext USING ls_base.
      ENDIF.


    ENDIF.

    " CHARACTERISTIC ROW
    IF gv_show_char = abap_true
    AND ls_base-merknr IS NOT INITIAL.

      IF ls_base-merknr <> lv_last_merknr.

        CLEAR ls_disp.
        MOVE-CORRESPONDING ls_base TO ls_disp.
        ls_disp-row_kind = 'C'.

        " fill CI
        PERFORM fill_ci_fields USING ls_base-steuerkz CHANGING ls_disp.

        ls_color-color-col = 6.
        ls_color-color-int = 1.
        APPEND ls_color TO ls_disp-line_color.

        APPEND ls_disp TO gt_display.

        lv_last_merknr = ls_base-merknr.

      ENDIF.
      IF gv_show_ltxt = gc_true.
        PERFORM append_char_longtext USING ls_base.
      ENDIF.

    ENDIF.
  ENDLOOP.

ENDFORM.

FORM display_alv.

  IF gt_display IS INITIAL.
    MESSAGE s398(00) WITH 'No data to display'.
    RETURN.
  ENDIF.

  IF lo_alv IS INITIAL.

    TRY.
        cl_salv_table=>factory(
          IMPORTING r_salv_table = lo_alv
          CHANGING  t_table      = gt_display ).

        lo_columns = lo_alv->get_columns( ).
        lo_columns->set_optimize( abap_true ).

        "Enable row color
        lo_columns->set_color_column( 'LINE_COLOR' ).

      CATCH cx_salv_not_found cx_salv_data_error cx_salv_msg.
        RETURN.
    ENDTRY.

    "=== Layout ===
    lo_layout = lo_alv->get_layout( ).
    DATA(ls_key) = VALUE salv_s_layout_key( report = sy-repid ).
    lo_layout->set_key( ls_key ).
    lo_layout->set_default( abap_true ).
    lo_layout->set_save_restriction( if_salv_c_layout=>restrict_none ).

    "=== Toolbar Functions ===
    lo_funcs = lo_alv->get_functions( ).
    lo_funcs->set_all( abap_true ).

    "=== Standard SALV Row Selector ===
    DATA(lo_sel) = lo_alv->get_selections( ).
    lo_sel->set_selection_mode( if_salv_c_selection_mode=>row_column ).
    " ^^^ THIS SHOWS THE STANDARD SALV LEFT SELECT COLUMN

    "=== Register Events ===
    lo_events = lo_alv->get_event( ).
    SET HANDLER lcl_event_handler=>on_user_command FOR lo_events.
    SET HANDLER lcl_event_handler=>on_link_click   FOR lo_events.

  ELSE.

    lo_alv->refresh( ).
  ENDIF.

  PERFORM set_column_visibility_by_state.
  PERFORM set_pfstatus_from_state.
  PERFORM set_ci_headers.
  PERFORM apply_column_order.

  lo_alv->display( ).

ENDFORM.
