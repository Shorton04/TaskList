*&---------------------------------------------------------------------*
*& Include ZRE_A2R_VALID_TASKLIST_F01
*&---------------------------------------------------------------------*
*---------------------------------------------------------------------*
* EVENT HANDLER IMPLEMENTATION
*---------------------------------------------------------------------*
CLASS lcl_event_handler IMPLEMENTATION.
  METHOD on_user_command.

    CASE e_salv_function.

      WHEN gc_func_clist_s.     " &CLISTS
        gv_show_char = gc_true.
        gv_show_ltxt = gc_false.

      WHEN gc_func_clist_h.     " &CLISTH
        gv_show_char = gc_false.
        gv_show_ltxt = gc_false.

      WHEN gc_func_ltxt_s.      " &LTXTS
        gv_show_ltxt = gc_true.

      WHEN gc_func_ltxt_h.      " &LTXTH
        gv_show_ltxt = gc_false.

      WHEN 'SELALL'.
        LOOP AT gt_display ASSIGNING FIELD-SYMBOL(<fs_disp_selall>).
          <fs_disp_selall>-sel = '[X]'.
        ENDLOOP.
        lo_alv->refresh( ).

      WHEN 'DESEL'.
        LOOP AT gt_display ASSIGNING FIELD-SYMBOL(<fs_disp_desel>).
          <fs_disp_desel>-sel = '[ ]'.
        ENDLOOP.

      WHEN OTHERS.
        RETURN.

    ENDCASE.

    " Rebuild data and refresh ALV / PF-STATUS / title
    PERFORM build_display.
    PERFORM set_pfstatus_from_state.
    PERFORM set_title_from_state.
    PERFORM set_column_visibility_by_state.

    IF lo_alv IS BOUND.
      lo_alv->refresh( ).
    ENDIF.

  ENDMETHOD.

  METHOD on_link_click.

    READ TABLE gt_display ASSIGNING FIELD-SYMBOL(<ls_disp>) INDEX row.
    IF sy-subrc = 0.

      "Toggle checkbox
      IF <ls_disp>-sel = 'X'.
        <ls_disp>-sel = ''.
      ELSE.
        <ls_disp>-sel = 'X'.
      ENDIF.

      lo_alv->refresh( ).

    ENDIF.

  ENDMETHOD.
ENDCLASS.

*---------------------------------------------------------------------*
* Validate Selection
*---------------------------------------------------------------------*
FORM validate_selection.

  "At least one TL type
  IF p_ftl IS INITIAL AND p_etl IS INITIAL AND p_gtl IS INITIAL.
    MESSAGE e398(00) WITH 'Select at least one Task List Type'.
  ENDIF.

  "Key date required
  IF so_keydt IS INITIAL.
    MESSAGE e398(00) WITH 'Enter a key date'.
  ENDIF.

ENDFORM.


*---------------------------------------------------------------------*
* PF-STATUS DETERMINATION
*---------------------------------------------------------------------*
FORM set_pfstatus_from_state.

  DATA lv_pf TYPE sypfkey.

  IF gv_show_char = gc_false AND gv_show_ltxt = gc_false.
    lv_pf = 'ZSALV_BOTH_HIDE'.
  ELSEIF gv_show_char = gc_false AND gv_show_ltxt = gc_true.
    lv_pf = 'ZSALV_LTXT_SHOW'.
  ELSEIF gv_show_char = gc_true AND gv_show_ltxt = gc_false.
    lv_pf = 'ZSALV_CLIST_ONLY'.
  ELSE.
    lv_pf = 'ZSALV_CLIST_LTXT'.
  ENDIF.

  IF lo_alv IS BOUND.
    lo_alv->set_screen_status(
      pfstatus = lv_pf
      report   = sy-repid ).
  ENDIF.
  IF gv_show_ltxt = gc_true.
    "Disable Show Characteristics button
    lo_alv->set_screen_status(
       pfstatus = lv_pf
       report   = sy-repid
    ).
    RETURN.
  ENDIF.

ENDFORM.

*---------------------------------------------------------------------*
* Title update
*---------------------------------------------------------------------*
FORM set_title_from_state.

  DATA lv_title TYPE string.
  lv_title = 'Maintenance Task List Report'.

  IF gv_show_char = gc_true.
    lv_title = lv_title && ' - Characteristics Active'.
  ENDIF.

  IF gv_show_ltxt = gc_true.
    lv_title = lv_title && ' - Long Text Active'.
  ENDIF.

  SET TITLEBAR 'TITLE_100' WITH lv_title.

ENDFORM.


*---------------------------------------------------------------------*
* Derive CI flags from PLMK-STEUERKZ
*---------------------------------------------------------------------*
FORM derive_ci_fields
  USING    iv_steuerkz TYPE plmk-steuerkz
  CHANGING cs_disp      TYPE ty_task_disp.

  DATA lv_steuer TYPE string.

  "Move STEUERKZ into local string
  lv_steuer = iv_steuerkz.

  "Pad right side to exactly 28 characters
  IF strlen( lv_steuer ) < 28.
    lv_steuer = lv_steuer && repeat( val = ' ' occ = 28 - strlen( lv_steuer ) ).
  ELSEIF strlen( lv_steuer ) > 28.
    lv_steuer = lv_steuer(28).
  ENDIF.

  "Assign each character into CI fields
  cs_disp-ci1  = lv_steuer+0(1).
  cs_disp-ci2  = lv_steuer+1(1).
  cs_disp-ci3  = lv_steuer+2(1).
  cs_disp-ci4  = lv_steuer+3(1).
  cs_disp-ci5  = lv_steuer+4(1).
  cs_disp-ci6  = lv_steuer+5(1).
  cs_disp-ci7  = lv_steuer+6(1).
  cs_disp-ci8  = lv_steuer+7(1).
  cs_disp-ci9  = lv_steuer+8(1).
  cs_disp-ci10 = lv_steuer+9(1).
  cs_disp-ci11 = lv_steuer+10(1).
  cs_disp-ci12 = lv_steuer+11(1).
  cs_disp-ci13 = lv_steuer+12(1).
  cs_disp-ci14 = lv_steuer+13(1).
  cs_disp-ci15 = lv_steuer+14(1).
  cs_disp-ci16 = lv_steuer+15(1).
  cs_disp-ci17 = lv_steuer+16(1).
  cs_disp-ci18 = lv_steuer+17(1).
  cs_disp-ci19 = lv_steuer+18(1).
  cs_disp-ci20 = lv_steuer+19(1).
  cs_disp-ci21 = lv_steuer+20(1).
  cs_disp-ci22 = lv_steuer+21(1).
  cs_disp-ci23 = lv_steuer+22(1).
  cs_disp-ci24 = lv_steuer+23(1).
  cs_disp-ci25 = lv_steuer+24(1).
  cs_disp-ci26 = lv_steuer+25(1).
  cs_disp-ci27 = lv_steuer+26(1).
  cs_disp-ci28 = lv_steuer+27(1).

ENDFORM.

*---------------------------------------------------------------------*
* GET_DATA
*---------------------------------------------------------------------*
FORM get_data.

  DATA: lv_keydat TYPE plko-datuv,
        lv_tolun  TYPE plmk-toleranzun,
        lv_tolob  TYPE plmk-toleranzob.

  CLEAR gt_base.
  CLEAR gs_base.

  lv_keydat = so_keydt.

  " Convert tolerance CHAR16 → FLTP
  CLEAR lv_tolun.
  CLEAR lv_tolob.
  IF so_tolun IS NOT INITIAL.
    lv_tolun = so_tolun.
  ENDIF.
  IF so_tolob IS NOT INITIAL.
    lv_tolob = so_tolob.
  ENDIF.

  "===================================================================*
  "* FDS JOIN WITH ALL FIELDS REQUIRED IN TY_TASK_BASE
  "===================================================================*
  SELECT
    "================ HEADER (PLKO) ==================================
    plko~plnty,
    plko~plnnr,
    plko~plnal,
    plko~datuv,
    plko~valid_to           AS valid_to,
    plko~werks,
    plko~ktext,
    plko~verwe,
    plko~vagrp,
    plko~statu              AS statu_h,
    plko~anlzu              AS anlzu_h,
    plko~strat,
    plko~istru              AS istru_h,
    plko~adpsp,
    plko~slwbez,
    plko~loekz              AS loekz_h,
    plko~andat,
    plko~annam,
    plko~aedat,
    plko~aenam,

    "================ FLOC / EQUIP (TAPL/EAPL) =======================
    tapl~tplnr,
    eapl~equnr,

    crhd_h~arbpl AS arbpl_h,

    "================ OPERATION (PLPO) ===============================
    plpo~plnkn,
    plpo~zaehl,
    plpo~datuv              AS datuv_o,
    plpo~valid_to           AS valid_to_o,
    plpo~vornr,
    plpo~ltxa1,
    plpo~steus,
    crhd~arbpl              AS arbpl_o,
    crhd~werks              AS werks_o,
    plpo~ktsch,
    plpo~arbei,
    plpo~arbeh,
    plpo~dauno,
    plpo~daune,
    plpo~kalid,
    plpo~execution_stage,
    plpo~anlzu              AS anlzu_o,
    plpo~istru              AS istru_o,
    plpo~larnt,
    plpo~ebeln,
    plpo~ebelp,
    plpo~lifnr,
    plpo~preis,
    plpo~waers,
    plpo~sakto,
    plpo~ekorg,
    plpo~ekgrp,
    plpo~txtsp              AS txtsp_op,
    plpo~loekz              AS loekz_o,

    "================ CHARACTERISTIC (PLMK) ==========================
    plmk~merknr,
    plmk~verwmerkm,
    plmk~kurztext           AS kurzt_char,
    plmk~ltextkz,
    plmk~toleranzun,
    plmk~toleranzob,
    plmk~pruefeinh,
    plmk~stichprver,
    plmk~pmethode,
    plmk~katalgart1,
    plmk~auswmenge1,
    plmk~steuerkz,
    plmk~loekz              AS loekz_c,
    plmk~gueltigab          AS gueltigab,
    plmk~valid_to_on_db     AS valid_to_c

    FROM  plko
      INNER JOIN plas
        ON plas~plnty = plko~plnty
       AND plas~plnnr = plko~plnnr
       AND plas~plnal = plko~plnal
       AND plas~loekz = ' '
       AND plas~datuv <= @lv_keydat
       AND plas~valid_to >= @lv_keydat

      INNER JOIN plpo
        ON plpo~plnty = plas~plnty
       AND plpo~plnnr = plas~plnnr
       AND plpo~plnkn = plas~plnkn
       AND plpo~zaehl = plas~zaehl
       AND plpo~loekz = ' '

      LEFT JOIN plmk
        ON plmk~plnty = plpo~plnty
       AND plmk~plnnr = plpo~plnnr
       AND plmk~plnkn = plpo~plnkn
       "AND plmk~zaehl = plpo~zaehl
       AND plmk~loekz = ' '

      LEFT JOIN tapl
        ON tapl~plnty = plko~plnty
       AND tapl~plnnr = plko~plnnr
       AND tapl~plnal = plko~plnal
       AND tapl~loekz = ' '

      LEFT JOIN eapl
        ON eapl~plnty = plko~plnty
       AND eapl~plnnr = plko~plnnr
       AND eapl~plnal = plko~plnal
       AND eapl~loekz = ' '

      LEFT JOIN crhd
        ON crhd~objid = plpo~arbid

      LEFT JOIN crhd AS crhd_h
      ON crhd_h~objid = plko~arbid
      AND crhd_h~objty = plko~arbty

    WHERE plko~loekz = ' '
      AND plko~datuv <= @lv_keydat
      AND plko~valid_to >= @lv_keydat
      AND plpo~datuv <= @lv_keydat
      AND plpo~valid_to >= @lv_keydat
      AND ( plmk~gueltigab IS INITIAL OR plmk~gueltigab <= @lv_keydat )
      AND ( plmk~valid_to_on_db IS INITIAL OR plmk~valid_to_on_db >= @lv_keydat )
          INTO CORRESPONDING FIELDS OF TABLE @gt_base.


  SORT gt_base BY plnty plnnr plnal vornr merknr.
  DELETE ADJACENT DUPLICATES FROM gt_base COMPARING plnty plnnr plnal vornr merknr.

  "===================================================================*
  "* ABAP-LEVEL FILTERING BASED ON SELECTION-SCREEN
  "===================================================================*
  DATA: lt_filtered TYPE STANDARD TABLE OF ty_task_base.

  CLEAR lt_filtered.

  LOOP AT gt_base INTO gs_base.

    IF gs_base-datuv > so_keydt.
      CONTINUE.
    ENDIF.

    IF gs_base-valid_to < so_keydt.
      CONTINUE.
    ENDIF.

    IF gs_base-datuv_o > so_keydt.
      CONTINUE.
    ENDIF.

    IF gs_base-valid_to_o < so_keydt.
      CONTINUE.
    ENDIF.

    IF gv_show_char = gc_true.
      IF gs_base-gueltigab > so_keydt.
        CONTINUE.
      ENDIF.

      IF gs_base-valid_to_c < so_keydt.
        CONTINUE.
      ENDIF.
    ENDIF.


    "===================================================================*
    "* TASK LIST TYPE FILTER (FTL / ETL / GTL)
    "===================================================================*
    IF gs_base-plnty = 'T' AND p_ftl IS INITIAL.
      CONTINUE.
    ENDIF.

    IF gs_base-plnty = 'E' AND p_etl IS INITIAL.
      CONTINUE.
    ENDIF.

    IF gs_base-plnty = 'A' AND p_gtl IS INITIAL.
      CONTINUE.
    ENDIF.

    IF so_arbph[] IS NOT INITIAL AND gs_base-arbpl_h NOT IN so_arbph.
      CONTINUE.
    ENDIF.


    "===================================================================*
    "* TASK LIST SELECTION FILTERS (PLNNR, PLNAL, FLOC, EQUIP)
    "===================================================================*
    IF so_plnnr[] IS NOT INITIAL AND gs_base-plnnr NOT IN so_plnnr.
      CONTINUE.
    ENDIF.

    IF so_plnal[] IS NOT INITIAL AND gs_base-plnal NOT IN so_plnal.
      CONTINUE.
    ENDIF.

    IF so_tplnr-low IS NOT INITIAL AND gs_base-tplnr NOT IN so_tplnr.
      CONTINUE.
    ENDIF.

    IF so_equnr-low IS NOT INITIAL AND gs_base-equnr NOT IN so_equnr.
      CONTINUE.
    ENDIF.


    "===================================================================*
    "* HEADER FILTERS — FDS (SAFE VERSION)
    "===================================================================*
    IF so_ktext[] IS NOT INITIAL AND gs_base-ktext NOT IN so_ktext.
      CONTINUE.
    ENDIF.

    IF so_werks-low IS NOT INITIAL AND gs_base-werks NOT IN so_werks.
      CONTINUE.
    ENDIF.

    IF so_verwe-low IS NOT INITIAL AND gs_base-verwe NOT IN so_verwe.
      CONTINUE.
    ENDIF.

    IF so_vagrp-low IS NOT INITIAL AND gs_base-vagrp NOT IN so_vagrp.
      CONTINUE.
    ENDIF.

    IF so_stath-low IS NOT INITIAL AND gs_base-statu_h NOT IN so_stath.
      CONTINUE.
    ENDIF.

    IF so_anlzh-low IS NOT INITIAL AND gs_base-anlzu_h NOT IN so_anlzh.
      CONTINUE.
    ENDIF.

    IF so_strat-low IS NOT INITIAL AND gs_base-strat NOT IN so_strat.
      CONTINUE.
    ENDIF.

    IF so_istrh-low IS NOT INITIAL AND gs_base-istru_h NOT IN so_istrh.
      CONTINUE.
    ENDIF.

    IF so_adpsp-low IS NOT INITIAL AND gs_base-adpsp NOT IN so_adpsp.
      CONTINUE.
    ENDIF.

    IF so_slwbe-low IS NOT INITIAL AND gs_base-slwbez NOT IN so_slwbe.
      CONTINUE.
    ENDIF.

    IF so_aedat-low IS NOT INITIAL AND gs_base-aedat NOT IN so_aedat.
      CONTINUE.
    ENDIF.

    IF so_aenam-low IS NOT INITIAL AND gs_base-aenam NOT IN so_aenam.
      CONTINUE.
    ENDIF.


    "===================================================================*
    "* OPERATION FILTERS
    "===================================================================*
    IF so_vornr[] IS NOT INITIAL AND gs_base-vornr NOT IN so_vornr.
      CONTINUE.
    ENDIF.

    IF so_otext[] IS NOT INITIAL AND gs_base-ltxa1 NOT IN so_otext.
      CONTINUE.
    ENDIF.

    IF so_steus[] IS NOT INITIAL AND gs_base-steus NOT IN so_steus.
      CONTINUE.
    ENDIF.

    IF so_arbpo[] IS NOT INITIAL AND gs_base-arbpl_o NOT IN so_arbpo.
      CONTINUE.
    ENDIF.

    IF so_ktsch[] IS NOT INITIAL AND gs_base-ktsch NOT IN so_ktsch.
      CONTINUE.
    ENDIF.

    IF so_exsta[] IS NOT INITIAL AND gs_base-execution_stage NOT IN so_exsta.
      CONTINUE.
    ENDIF.

    IF so_anlzo[] IS NOT INITIAL AND gs_base-anlzu_o NOT IN so_anlzo.
      CONTINUE.
    ENDIF.

    IF so_istru[] IS NOT INITIAL AND gs_base-istru_o NOT IN so_istru.
      CONTINUE.
    ENDIF.

    IF so_larnt[] IS NOT INITIAL AND gs_base-larnt NOT IN so_larnt.
      CONTINUE.
    ENDIF.

    IF so_ebeln[] IS NOT INITIAL AND gs_base-ebeln NOT IN so_ebeln.
      CONTINUE.
    ENDIF.

    IF so_ebelp[] IS NOT INITIAL AND gs_base-ebelp NOT IN so_ebelp.
      CONTINUE.
    ENDIF.

    IF so_lifnr[] IS NOT INITIAL AND gs_base-lifnr NOT IN so_lifnr.
      CONTINUE.
    ENDIF.


    "===================================================================*
    "* OPERATION LONG TEXT INDICATOR FILTER
    "===================================================================*
    IF p_oltin = 'X' AND gs_base-txtsp_op IS INITIAL.
      CONTINUE.
    ENDIF.


    "===================================================================*
    "* CHARACTERISTIC FILTERS (ONLY IF SHOW CHAR IS ON)
    "===================================================================*
    IF gv_show_char = gc_true.

      IF so_merkn-low IS NOT INITIAL AND gs_base-merknr NOT IN so_merkn.
        CONTINUE.
      ENDIF.

      IF so_mic-low IS NOT INITIAL AND gs_base-verwmerkm NOT IN so_mic.
        CONTINUE.
      ENDIF.

      IF so_cktxt-low IS NOT INITIAL AND gs_base-kurzt_char NOT IN so_cktxt.
        CONTINUE.
      ENDIF.

      IF so_ltxtk-low IS NOT INITIAL AND gs_base-ltextkz NOT IN so_ltxtk.
        CONTINUE.
      ENDIF.

      IF so_meth-low IS NOT INITIAL AND gs_base-pmethode NOT IN so_meth.
        CONTINUE.
      ENDIF.

      IF so_sproc-low IS NOT INITIAL AND gs_base-stichprver NOT IN so_sproc.
        CONTINUE.
      ENDIF.

    ENDIF.


    "===================================================================*
    "* TOLERANCE FILTERS
    "===================================================================*
    IF so_tolun IS NOT INITIAL AND gs_base-toleranzun <> so_tolun.
      CONTINUE.
    ENDIF.

    IF so_tolob IS NOT INITIAL AND gs_base-toleranzob <> so_tolob.
      CONTINUE.
    ENDIF.


    "===================================================================*
    "* PASSED ALL FILTERS
    "===================================================================*
    APPEND gs_base TO lt_filtered.

  ENDLOOP.

  gt_base = lt_filtered.

ENDFORM.

*&---------------------------------------------------------------------*
*& Module STATUS_0100 OUTPUT
*&---------------------------------------------------------------------*
MODULE status_0100 OUTPUT.

  PERFORM set_pfstatus_from_state.
  PERFORM set_title_from_state.

  "Create container + SALV only once
  IF go_container IS INITIAL.

    CREATE OBJECT go_container
      EXPORTING
        container_name = 'CC_ALV'.   "Custom Control name on Screen 0100

    "Initial build of display table
    PERFORM build_display.

    "Create SALV inside container
    cl_salv_table=>factory(
      EXPORTING
        r_container  = go_container
      IMPORTING
        r_salv_table = lo_alv
      CHANGING
        t_table      = gt_display ).

    "Configure and display ALV in container
    PERFORM display_alv.

  ELSE.
    "Dataset might have changed between calls
    IF lo_alv IS BOUND.
      lo_alv->refresh( ).
    ENDIF.
  ENDIF.

ENDMODULE.


*&---------------------------------------------------------------------*
*& Form build_display
*&---------------------------------------------------------------------*
FORM build_display.

  DATA: ls_base        TYPE ty_task_base,
        ls_disp        TYPE ty_task_disp,
        lv_last_plnty  TYPE plnty,
        lv_last_plnnr  TYPE plnnr,
        lv_last_plnal  TYPE plnal,
        lv_last_vornr  TYPE plnkn,
        lv_last_merknr TYPE merknr,
        ls_color       TYPE lvc_s_scol.

  CLEAR: gt_display,
         lv_last_plnty,
         lv_last_plnnr,
         lv_last_plnal,
         lv_last_vornr,
         lv_last_merknr.

  SORT gt_base BY plnty plnnr plnal vornr merknr.

  LOOP AT gt_base INTO ls_base.

    " NEW OPERATION GROUP
    IF ls_base-vornr <> lv_last_vornr
    OR ls_base-plnnr <> lv_last_plnnr
    OR ls_base-plnal <> lv_last_plnal
    OR ls_base-plnty <> lv_last_plnty.

      CLEAR ls_disp.
      MOVE-CORRESPONDING ls_base TO ls_disp.
      ls_disp-row_kind = 'O'.

      " Fill CI fields here
      PERFORM fill_ci_fields USING ls_base-steuerkz CHANGING ls_disp.

      APPEND ls_disp TO gt_display.

      lv_last_plnty = ls_base-plnty.
      lv_last_plnnr = ls_base-plnnr.
      lv_last_plnal = ls_base-plnal.
      lv_last_vornr = ls_base-vornr.

      CLEAR lv_last_merknr.

    ENDIF.

    " CHARACTERISTIC ROW
    IF gv_show_char = abap_true
    AND ls_base-merknr IS NOT INITIAL.

      IF ls_base-merknr <> lv_last_merknr.

        CLEAR ls_disp.
        MOVE-CORRESPONDING ls_base TO ls_disp.
        ls_disp-row_kind = 'C'.

        " fill CI
        PERFORM fill_ci_fields USING ls_base-steuerkz CHANGING ls_disp.

        ls_color-color-col = 6.
        ls_color-color-int = 1.
        APPEND ls_color TO ls_disp-line_color.

        APPEND ls_disp TO gt_display.

        lv_last_merknr = ls_base-merknr.

      ENDIF.

    ENDIF.

  ENDLOOP.

ENDFORM.



************************************************************************
* FORM fill_ci_fields – fills CI01..CI28 from steuerkz
************************************************************************
FORM fill_ci_fields
  USING    pv_steuerkz TYPE plmk-steuerkz
  CHANGING ps_disp     TYPE ty_task_disp.

  ps_disp-ci1  = pv_steuerkz+0(1).
  ps_disp-ci2  = pv_steuerkz+1(1).
  ps_disp-ci3  = pv_steuerkz+2(1).
  ps_disp-ci4  = pv_steuerkz+3(1).
  ps_disp-ci5  = pv_steuerkz+4(1).
  ps_disp-ci6  = pv_steuerkz+5(1).
  ps_disp-ci7  = pv_steuerkz+6(1).
  ps_disp-ci8  = pv_steuerkz+7(1).
  ps_disp-ci9  = pv_steuerkz+8(1).
  ps_disp-ci10 = pv_steuerkz+9(1).
  ps_disp-ci11 = pv_steuerkz+10(1).
  ps_disp-ci12 = pv_steuerkz+11(1).
  ps_disp-ci13 = pv_steuerkz+12(1).
  ps_disp-ci14 = pv_steuerkz+13(1).
  ps_disp-ci15 = pv_steuerkz+14(1).
  ps_disp-ci16 = pv_steuerkz+15(1).
  ps_disp-ci17 = pv_steuerkz+16(1).
  ps_disp-ci18 = pv_steuerkz+17(1).
  ps_disp-ci19 = pv_steuerkz+18(1).
  ps_disp-ci20 = pv_steuerkz+19(1).
  ps_disp-ci21 = pv_steuerkz+20(1).
  ps_disp-ci22 = pv_steuerkz+21(1).
  ps_disp-ci23 = pv_steuerkz+22(1).
  ps_disp-ci24 = pv_steuerkz+23(1).
  ps_disp-ci25 = pv_steuerkz+24(1).
  ps_disp-ci26 = pv_steuerkz+25(1).
  ps_disp-ci27 = pv_steuerkz+26(1).
  ps_disp-ci28 = pv_steuerkz+27(1).

ENDFORM.


FORM set_ci_headers.

  DATA: lo_columns TYPE REF TO cl_salv_columns_table,
        lo_col     TYPE REF TO cl_salv_column_table.

  TRY.
      lo_columns = lo_alv->get_columns( ).
    CATCH cx_salv_msg.
      RETURN.
  ENDTRY.

  "----------------------------------------------
  " Helper: convert literal text safely (CHAR40)
  "----------------------------------------------
  DEFINE set_hdr.
    TRY.
        lo_col ?= lo_columns->get_column( &1 ).

        lo_col->set_short_text(  CONV #( &2 ) ). "CHAR10/20/40 all accepted
        lo_col->set_medium_text( CONV #( &2 ) ).
        lo_col->set_long_text(   CONV #( &2 ) ).

      CATCH cx_salv_not_found.
    ENDTRY.
  END-OF-DEFINITION.

  "----------------------------------------------
  " CI01 – CI28 Headers
  "----------------------------------------------
  set_hdr 'CI1'  'Quantitative Char'.
  set_hdr 'CI2'  'Record Values'.
  set_hdr 'CI3'  'Ref Char Required'.
  set_hdr 'CI4'  'Upper Spec Limit'.
  set_hdr 'CI5'  'Lower Spec Limit'.
  set_hdr 'CI6'  'Check Target'.
  set_hdr 'CI7'  'Inspection Scope'.
  set_hdr 'CI8'  'Long Term Insp'.
  set_hdr 'CI9'  'Recording Type'.
  set_hdr 'CI10' 'Document Required'.
  set_hdr 'CI11' 'Characteristic Cat'.
  set_hdr 'CI12' 'Sync Active'.
  set_hdr 'CI13' 'Sample Qty Add'.
  set_hdr 'CI14' 'Destructive Insp'.
  set_hdr 'CI15' 'Calculated Char'.
  set_hdr 'CI16' 'Sampling Proc Req'.
  set_hdr 'CI17' 'Quality Score Rel'.
  set_hdr 'CI18' 'No Changes Allowed'.
  set_hdr 'CI19' 'Defects Count'.
  set_hdr 'CI20' 'QM Subsystem Char'.
  set_hdr 'CI21' 'Specs Changeable'.
  set_hdr 'CI22' 'Test Equip Req'.
  set_hdr 'CI23' 'Auto Defect Rec'.
  set_hdr 'CI24' 'Change Docs Req'.
  set_hdr 'CI25' 'SPC Characteristic'.
  set_hdr 'CI26' 'Print Indicator'.
  set_hdr 'CI27' 'Parameter Char'.
  set_hdr 'CI28' 'Process Char'.

ENDFORM.

*&---------------------------------------------------------------------*
*& Form ADJUST_DISPLAY_FIELDS_BY_STATE
*&---------------------------------------------------------------------*
FORM adjust_display_fields_by_state.

  FIELD-SYMBOLS: <ls_disp> TYPE ty_task_disp.

  LOOP AT gt_display ASSIGNING <ls_disp>.

    "-------------------------------------------------------------*
    " A) Show Char = No, Show Long Text = No
    "    → Operations only
    "    → Insp# + Description cleared on op rows
    "    → No long text fields used
    "-------------------------------------------------------------*
    IF gv_show_char = gc_false AND gv_show_ltxt = gc_false.

      "Clear any long-text leftovers (even if columns are hidden)
      CLEAR: <ls_disp>-tdformat,
             <ls_disp>-op_ltxt,
             <ls_disp>-char_ltxt.

      IF <ls_disp>-row_kind = 'O'.
        CLEAR: <ls_disp>-merknr,      "Inspection number
               <ls_disp>-kurzt_char.  "Inspection description
        "PMETHODE stays, per FDS
      ENDIF.

      "-------------------------------------------------------------*
      " B) Show Char = Yes, Show Long Text = No
      "    → Operation + characteristic rows, no long text
      "    → Keep Insp#, Description, Method
      "-------------------------------------------------------------*
    ELSEIF gv_show_char = gc_true AND gv_show_ltxt = gc_false.

      "No long text in this mode
      CLEAR: <ls_disp>-tdformat,
             <ls_disp>-op_ltxt,
             <ls_disp>-char_ltxt.

      "-------------------------------------------------------------*
      " C) Show Char = No, Show Long Text = Yes
      "    → Operation rows + operation long-text rows
      "    → FK + OP_LTXT visible
      "    → No characteristic info at all
      "-------------------------------------------------------------*
    ELSEIF gv_show_char = gc_false AND gv_show_ltxt = gc_true.

      CLEAR: <ls_disp>-merknr,       "Insp#
             <ls_disp>-kurzt_char,   "Description
             <ls_disp>-pmethode,     "Method
             <ls_disp>-char_ltxt.    "Char long text

      "TDFORMAT + OP_LTXT stay filled for op long-text rows

      "-------------------------------------------------------------*
      " D) Show Char = Yes, Show Long Text = Yes
      "    → Operation + characteristic rows + characteristic long text
      "    → OP long text is not generated in this mode
      "-------------------------------------------------------------*
    ELSE.
      "No additional clearing needed.
      "APPEND_CHAR_LONGTEXT already clears OP_LTXT for char rows.
    ENDIF.

  ENDLOOP.

ENDFORM.


*---------------------------------------------------------------------*
* Display ALV
*---------------------------------------------------------------------*
FORM display_alv.

  IF gt_display IS INITIAL.
    MESSAGE s398(00) WITH 'No data to display'.
    RETURN.
  ENDIF.

  IF lo_alv IS INITIAL.

    cl_salv_table=>factory(
      IMPORTING
        r_salv_table = lo_alv
      CHANGING
        t_table      = gt_display ).

    lo_columns = lo_alv->get_columns( ).
    lo_columns->set_optimize( abap_true ).

    TRY.
        lo_columns->set_color_column( 'LINE_COLOR' ).
      CATCH cx_salv_not_found cx_salv_data_error.
    ENDTRY.

    "==================================================
    " Enable layout saving
    "==================================================
    lo_layout = lo_alv->get_layout( ).
    DATA(ls_key) = VALUE salv_s_layout_key( report = sy-repid ).
    lo_layout->set_key( ls_key ).
    lo_layout->set_default( abap_true ).
    lo_layout->set_save_restriction( if_salv_c_layout=>restrict_none ).

    "==================================================
    " Enable toolbar buttons
    "==================================================
    lo_funcs = lo_alv->get_functions( ).
    lo_funcs->set_all( abap_true ).

    "==================================================
    " Enable MULTIPLE selection (old SALV)
    "==================================================
    DATA(lo_sel) = lo_alv->get_selections( ).
    lo_sel->set_selection_mode( if_salv_c_selection_mode=>multiple ).

    "==================================================
    " Make SEL a clickable checkbox
    "==================================================
    TRY.
        DATA(lo_col) = lo_columns->get_column( 'SEL' ).
        lo_col->set_short_text( ' ' ).
        lo_col->set_medium_text( 'Select' ).
        lo_col->set_long_text( 'Select' ).
      CATCH cx_salv_not_found.
    ENDTRY.

    "==================================================
    " Register hotspot click + button events
    "==================================================
    lo_events = lo_alv->get_event( ).

    SET HANDLER lcl_event_handler=>on_user_command FOR lo_events.
    SET HANDLER lcl_event_handler=>on_link_click   FOR lo_events.

  ELSE.
    lo_alv->refresh( ).
  ENDIF.

  PERFORM set_position USING 'SEL' 1.

  PERFORM set_column_visibility_by_state.
  PERFORM set_pfstatus_from_state.

  lo_alv->display( ).

ENDFORM.

*&---------------------------------------------------------------------*
*& Form APPEND_OP_LONGTEXT
*&---------------------------------------------------------------------*
*  - Appends operation long text lines as separate rows in GT_DISPLAY
*    with:
*       ROW_KIND = 'L'
*       OP_LTXT  = text line
*    (other key fields copied from IS_BASE)
*&---------------------------------------------------------------------*
FORM append_op_longtext USING is_base TYPE ty_task_base.

  DATA: lt_lines  TYPE STANDARD TABLE OF tline,
        ls_line   TYPE tline,
        lv_name   TYPE thead-tdname,
        lv_indent TYPE string,
        ls_color  TYPE lvc_s_scol.

  CLEAR: lt_lines, lv_name.

  CONCATENATE sy-mandt
              is_base-plnty
              is_base-plnnr
              is_base-plnkn
              is_base-zaehl
         INTO lv_name.

  CALL FUNCTION 'READ_TEXT'
    EXPORTING
      client   = sy-mandt
      id       = 'PLPO'
      language = sy-langu
      name     = lv_name
      object   = 'ROUTING'
    TABLES
      lines    = lt_lines
    EXCEPTIONS
      OTHERS   = 4.

  IF sy-subrc <> 0 OR lt_lines IS INITIAL.
    RETURN.
  ENDIF.

  LOOP AT lt_lines INTO ls_line.

    CLEAR gs_display.
    MOVE-CORRESPONDING is_base TO gs_display.

    gs_display-row_kind = 'L'.
    gs_display-op_ltxt  = ls_line-tdline.
    gs_display-tdformat = ls_line-tdformat.

    CONCATENATE lv_indent gs_display-op_ltxt INTO gs_display-op_ltxt.

    "GREEN color
    CLEAR ls_color.
    ls_color-color-col = 1.
    APPEND ls_color TO gs_display-line_color.

    APPEND gs_display TO gt_display.

  ENDLOOP.

ENDFORM.



*&---------------------------------------------------------------------*
*& Form APPEND_CHAR_LONGTEXT
*&---------------------------------------------------------------------*
*  - Appends characteristic long text lines as separate rows in GT_DISPLAY
*    with:
*       ROW_KIND  = 'L'
*       CHAR_LTXT = text line
*&---------------------------------------------------------------------*
FORM append_char_longtext USING is_base TYPE ty_task_base.

  DATA: lt_lines  TYPE STANDARD TABLE OF tline,
        ls_line   TYPE tline,
        lv_name   TYPE thead-tdname,
        lv_tmp    TYPE thead-tdname,
        lv_indent TYPE string,
        lv_merknr TYPE char08,
        ls_color  TYPE lvc_s_scol.

  CLEAR: lt_lines, lv_name, lv_tmp.

  IF is_base-ltextkz IS INITIAL.
    RETURN.
  ENDIF.

  lv_merknr = is_base-merknr.

  CONCATENATE sy-mandt
              is_base-plnty
              is_base-plnnr
              is_base-plnkn
         INTO lv_tmp.

  CONCATENATE lv_tmp is_base-merknr INTO lv_tmp SEPARATED BY space.
  CONCATENATE lv_tmp is_base-zaehl INTO lv_name.

  CALL FUNCTION 'READ_TEXT'
    EXPORTING
      client   = sy-mandt
      id       = 'QM'
      language = sy-langu
      name     = lv_name
      object   = 'QSS'
    TABLES
      lines    = lt_lines
    EXCEPTIONS
      OTHERS   = 8.

  IF sy-subrc <> 0 OR lt_lines IS INITIAL.
    RETURN.
  ENDIF.

  LOOP AT lt_lines INTO ls_line.

    CLEAR gs_display.
    MOVE-CORRESPONDING is_base TO gs_display.

    gs_display-row_kind  = 'L'.
    gs_display-char_ltxt = ls_line-tdline.
    gs_display-tdformat  = ls_line-tdformat.

    CONCATENATE lv_indent gs_display-char_ltxt INTO gs_display-char_ltxt.

    CLEAR gs_display-op_ltxt.

    "GREEN color
    CLEAR ls_color.
    ls_color-color-col = 4.
    APPEND ls_color TO gs_display-line_color.

    APPEND gs_display TO gt_display.

  ENDLOOP.

ENDFORM.



*======================================================================*
*   Toggle helpers for PF-STATUS buttons
*======================================================================*

*&---------------------------------------------------------------------*
*& Form toggle_char
*&---------------------------------------------------------------------*
FORM toggle_char.

  IF gv_show_char = gc_true.
    gv_show_char = gc_false.
  ELSE.
    gv_show_char = gc_true.
  ENDIF.

  PERFORM build_display.
  PERFORM set_pfstatus_from_state.
  PERFORM set_title_from_state.
  PERFORM set_column_visibility_by_state.

  IF lo_alv IS BOUND.
    lo_alv->refresh( ).
  ENDIF.

ENDFORM.

*&---------------------------------------------------------------------*
*& Form toggle_ltxt
*&---------------------------------------------------------------------*
FORM toggle_ltxt.

  IF gv_show_ltxt = gc_true.
    gv_show_ltxt = gc_false.
  ELSE.
    gv_show_ltxt = gc_true.
  ENDIF.

  PERFORM build_display.
  PERFORM set_pfstatus_from_state.
  PERFORM set_title_from_state.
  PERFORM set_column_visibility_by_state.

  IF lo_alv IS BOUND.
    lo_alv->refresh( ).
  ENDIF.

ENDFORM.


*---------------------------------------------------------------------*
* USER COMMAND (ONLY FOR BACK / EXIT / REFRESH)
*---------------------------------------------------------------------*
MODULE user_command_0100 INPUT.

  CASE sy-ucomm.

    WHEN 'REFRESH'.
      PERFORM build_display.
      PERFORM set_column_visibility_by_state.
      IF lo_alv IS BOUND.
        lo_alv->refresh( ).
      ENDIF.

    WHEN '&F03' OR '&F12' OR '&F15'.
      LEAVE TO SCREEN 0.

  ENDCASE.

ENDMODULE.

*---------------------------------------------------------------------*
* Helper: hide a column (technical)
*---------------------------------------------------------------------*
FORM hide_column USING pv_col TYPE lvc_fname.
  TRY.
      lo_column ?= lo_columns->get_column( pv_col ).

      "Hide from initial display but still available in Change Layout
      lo_column->set_visible( abap_false ).
      lo_column->set_technical( abap_false ).  "IMPORTANT

    CATCH cx_salv_not_found.
  ENDTRY.
ENDFORM.

*---------------------------------------------------------------------*
* Helper: set column position (uses columns->set_column_position)
*---------------------------------------------------------------------*
FORM set_position USING pv_field pv_pos.

  TRY.
      DATA(lo_col) = lo_columns->get_column( pv_field ).
      lo_columns->set_column_position(
        columnname = pv_field
        position   = pv_pos ).
    CATCH cx_salv_not_found.
  ENDTRY.

ENDFORM.

*---------------------------------------------------------------------*
* Helper: set column technical flag (visible / hidden)
*---------------------------------------------------------------------*
FORM set_column_technical USING pv_col  TYPE lvc_fname
                                pv_tech TYPE abap_bool.

  DATA: lo_col TYPE REF TO cl_salv_column_table.

  IF lo_alv IS INITIAL.
    RETURN.
  ENDIF.

  " Ensure we have the latest columns handle
  lo_columns = lo_alv->get_columns( ).

  TRY.
      lo_col ?= lo_columns->get_column( pv_col ).
      lo_col->set_technical( pv_tech ).
    CATCH cx_salv_not_found.
      " Ignore if column not in structure
  ENDTRY.

ENDFORM.


*&---------------------------------------------------------------------*
*& Form SET_COLUMN_VISIBILITY_BY_STATE
*&---------------------------------------------------------------------*
FORM set_column_visibility_by_state.

  DATA lo_columns TYPE REF TO cl_salv_columns_table.
  DATA lo_col     TYPE REF TO cl_salv_column_table.

  IF lo_alv IS INITIAL.
    RETURN.
  ENDIF.

  lo_columns = lo_alv->get_columns( ).

  " ===== Hide CI1–CI28 but NOT technical =====
  DO 28 TIMES.
    DATA lv_ci TYPE lvc_fname.
    lv_ci = |CI{ sy-index WIDTH = 2 PAD = '0' }|.

    TRY.
        lo_col ?= lo_columns->get_column( lv_ci ).
        lo_col->set_visible( abap_false ).   "Hidden in ALV
        lo_col->set_technical( abap_false ). "Visible in Change Layout
      CATCH cx_salv_not_found.
    ENDTRY.
  ENDDO.

  IF lo_alv IS INITIAL.
    RETURN.
  ENDIF.

  lo_columns = lo_alv->get_columns( ).

  "--------------------------------------------------------------
  " MACROS
  "--------------------------------------------------------------
  DEFINE hide_col.
    TRY.
        lo_col ?= lo_columns->get_column( &1 ).
        lo_col->set_visible( abap_false ).
        lo_col->set_technical( abap_false ).
      CATCH cx_salv_not_found.
    ENDTRY.
  END-OF-DEFINITION.

  DEFINE show_col.
    TRY.
        lo_col ?= lo_columns->get_column( &1 ).
        lo_col->set_visible( abap_true ).
        lo_col->set_technical( abap_false ).
      CATCH cx_salv_not_found.
    ENDTRY.
  END-OF-DEFINITION.

  "==============================================================
  " ALWAYS SHOW CORE FDS COLUMNS
  "==============================================================
  show_col 'SEL'.
  show_col 'PLNNR'.
  show_col 'PLNAL'.
  show_col 'WERKS'.
  show_col 'VORNR'.
  show_col 'ARBPL_O'.
  show_col 'ARBEI'.
  show_col 'KURZT_CHAR'.
  show_col 'PMETHODE'.
  show_col 'MERKNR'.
  show_col 'TDFORMAT'.
  show_col 'OP_LTXT'.
  show_col 'CHAR_LTXT'.

  "==============================================================
  " CI COLUMNS — ALWAYS SHOW WITH HEADER
  "==============================================================

  "==============================================================
  " CASE LOGIC A/B/C/D
  "==============================================================
  IF gv_show_char = abap_false AND gv_show_ltxt = abap_false.
    hide_col 'OP_LTXT'.
    hide_col 'CHAR_LTXT'.

  ELSEIF gv_show_char = abap_true AND gv_show_ltxt = abap_false.
    hide_col 'OP_LTXT'.
    hide_col 'CHAR_LTXT'.

  ELSEIF gv_show_char = abap_false AND gv_show_ltxt = abap_true.
    show_col 'OP_LTXT'.
    hide_col 'CHAR_LTXT'.

  ELSE.
    hide_col 'OP_LTXT'.
    show_col 'CHAR_LTXT'.
  ENDIF.

ENDFORM.
